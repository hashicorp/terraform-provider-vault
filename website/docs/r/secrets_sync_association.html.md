---
layout: "vault"
page_title: "Vault: vault_secrets_sync_association resource"
sidebar_current: "docs-vault-resource-secrets-sync-association"
description: |-
  Triggers a sync operation in Vault and links a secret to an existing destination
---

# vault\_secrets\_sync\_association

Triggers a sync operation in Vault and links a secret to an existing destination.
Requires Vault 1.16+. *Available only for Vault Enterprise*.

~> **Important** All data provided in the resource configuration will be
written in cleartext to state and plan files generated by Terraform, and
will appear in the console output when Terraform runs. Protect these
artifacts accordingly. See
[the main provider documentation](../index.html)
for more details.

For more information on associations, please refer to the Vault
[documentation](https://developer.hashicorp.com/vault/docs/sync#associations).

## Example Usage

```hcl
resource "vault_mount" "kvv2" {
  path        = "kvv2"
  type        = "kv"
  options     = { version = "2" }
  description = "KV Version 2 secret engine mount"
}

resource "vault_kv_secret_v2" "token" {
  mount = vault_mount.kvv2.path
  name  = "token"
  data_json = jsonencode(
    {
      dev  = "B!gS3cr3t",
      prod = "S3cureP4$$"
    }
  )
}

resource "vault_secrets_sync_gh_destination" "gh" {
  name                 = "gh-dest"
  access_token         = var.access_token
  repository_owner     = var.repo_owner
  repository_name      = "repo-name-example"
  secret_name_template = "vault_{{ .MountAccessor | lowercase }}_{{ .SecretPath | lowercase }}"
}

resource "vault_secrets_sync_association" "gh_token" {
  name        = vault_secrets_sync_gh_destination.gh.name
  type        = vault_secrets_sync_gh_destination.gh.type
  mount       = vault_mount.kvv2.path
  secret_name = vault_kv_secret_v2.token.name
}
```

## Argument Reference

The following arguments are supported:

* `namespace` - (Optional) The namespace to provision the resource in.
  The value should not contain leading or trailing forward slashes.
  The `namespace` is always relative to the provider's configured [namespace](/docs/providers/vault/index.html#namespace).

* `name` - (Required) Specifies the name of the destination.

* `type` - (Required) Specifies the destination type.

* `mount` - (Required) Specifies the mount where the secret is located.

* `secret_name` - (Required) Specifies the name of the secret to synchronize.

## Attributes Reference

The following attributes are exported in addition to the above:

* `sync_status` - Specifies the status of the association (for eg. `SYNCED`).

* `updated_at` - Duration string specifying when the secret was last updated.
