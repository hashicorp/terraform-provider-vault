---
layout: "vault"
page_title: "Vault: vault_pki_secret_backend_external_ca_order_certificate resource"
sidebar_current: "docs-vault-resource-pki-secret-backend-external-ca-order-certificate"
description: |-
  Fetches the certificate from a completed ACME order.
---

# vault\_pki\_secret\_backend\_external\_ca\_order\_certificate

Polls the order status endpoint until the order is completed, then fetches the certificate. This resource waits for all ACME challenges to be validated and the certificate to be issued by the external CA.

~> **Important** All data provided in the resource configuration will be
written in cleartext to state and plan files generated by Terraform, and
will appear in the console output when Terraform runs. Protect these
artifacts accordingly. See
[the main provider documentation](../index.html)
for more details.

~> **Note** This resource should be used after all challenges have been fulfilled using `vault_pki_secret_backend_external_ca_order_challenge_fulfilled`. It will poll the order status and wait for completion before fetching the certificate.

## Example Usage

```hcl
resource "vault_mount" "pki" {
  path = "pki"
  type = "pki"
}

resource "vault_pki_secret_backend_acme_account" "example" {
  mount         = vault_mount.pki.path
  name          = "my-acme-account"
  directory_url = "https://acme-v02.api.letsencrypt.org/directory"
  email_contacts = [
    "admin@example.com"
  ]
}

resource "vault_pki_secret_backend_external_ca_role" "example" {
  mount             = vault_mount.pki.path
  name              = "example-role"
  acme_account_name = vault_pki_secret_backend_acme_account.example.name
  
  allowed_domains = ["example.com"]
  allowed_domain_options = ["bare_domains", "subdomains"]
}

resource "vault_pki_secret_backend_external_ca_order" "example" {
  mount     = vault_mount.pki.path
  role_name = vault_pki_secret_backend_external_ca_role.example.name
  
  identifiers = ["www.example.com"]
}

# Retrieve and fulfill challenges (simplified)
data "vault_pki_secret_backend_external_ca_order_challenge" "example" {
  mount          = vault_mount.pki.path
  role_name      = vault_pki_secret_backend_external_ca_role.example.name
  order_id       = vault_pki_secret_backend_external_ca_order.example.order_id
  challenge_type = "http-01"
  identifier     = "www.example.com"
}

# This is an example of fulfilling the http-01 challenge using pebble, an 
# acme testing tool which verifies challenges on port 5002 by default.
# A real acme server would require the http-01 challenge be fulfilled on
# port 80, of the real domain name for which the certificate is tied to.
resource "null_resource" "acme_challenge_server" {
  triggers = {
    token             = data.vault_pki_secret_backend_external_ca_order_challenge.example.token
    key_authorization = data.vault_pki_secret_backend_external_ca_order_challenge.example.key_authorization
  }

  provisioner "local-exec" {
    command = <<-EOT
(
      mkdir -p /tmp/acme-challenge/.well-known/acme-challenge
      /bin/echo -n '${data.vault_pki_secret_backend_external_ca_order_challenge.example.key_authorization}' > /tmp/acme-challenge/.well-known/acme-challenge/${data.vault_pki_secret_backend_external_ca_order_challenge.example.token}
      cd /tmp/acme-challenge 
      (nohup python3 -m http.server 5002 >/dev/null 2>&1) & 
      echo $! > /tmp/acme-challenge-server.pid
      sleep 2
) &
    EOT
  }

  provisioner "local-exec" {
    when    = destroy
    command = <<-EOT
      if [ -f /tmp/acme-challenge-server.pid ]; then
        kill $(cat /tmp/acme-challenge-server.pid) 2>/dev/null || true
        rm -f /tmp/acme-challenge-server.pid
      fi
      rm -rf /tmp/acme-challenge
    EOT
  }
}

resource "vault_pki_secret_backend_external_ca_order_challenge_fulfilled" "example" {
  mount          = vault_mount.pki.path
  role_name      = vault_pki_secret_backend_external_ca_role.example.name
  order_id       = vault_pki_secret_backend_external_ca_order.example.order_id
  challenge_type = "http-01"
  identifier     = "www.example.com"
  
  # Ensure challenge is deployed before marking as fulfilled
  depends_on = [null_resource.acme_challenge_server]
}

# Fetch the certificate after challenges are fulfilled
resource "vault_pki_secret_backend_external_ca_order_certificate" "example" {
  depends_on = [
    vault_pki_secret_backend_external_ca_order_challenge_fulfilled.example
  ]
  
  mount     = vault_mount.pki.path
  role_name = vault_pki_secret_backend_external_ca_role.example.name
  order_id  = vault_pki_secret_backend_external_ca_order.example.order_id
}

# Use the certificate
output "certificate" {
  value     = vault_pki_secret_backend_external_ca_order_certificate.example.certificate
}

output "private_key" {
  value     = vault_pki_secret_backend_external_ca_order_certificate.example.private_key
  sensitive = true
}
```

## Argument Reference

The following arguments are supported:

* `namespace` - (Optional) The namespace to provision the resource in.
  The value should not contain leading or trailing forward slashes.
  The `namespace` is always relative to the provider's configured [namespace](/docs/providers/vault/index.html#namespace).
   *Available only for Vault Enterprise*.

* `mount` - (Required) The path where the PKI External CA secret backend is mounted.

* `role_name` - (Required) Name of the role associated with the order.

* `order_id` - (Required) The unique identifier for the ACME order.

## Attributes Reference

In addition to the fields above, the following attributes are exported:

* `id` - The ID of the resource in the format `<mount>/role/<role_name>/order/<order_id>/certificate`.

* `certificate` - The PEM-encoded certificate issued by the external CA.

* `ca_chain` - List of PEM-encoded certificates in the CA chain.

* `private_key` - The PEM-encoded private key. This is only available if the order was created using the identifier workflow (not with a CSR). If a CSR was provided, the private key must be managed separately.

* `serial_number` - The serial number of the issued certificate.

## Import

PKI External CA order certificates can be imported using the format `<mount>/role/<role_name>/order/<order_id>/certificate`, e.g.

```
$ terraform import vault_pki_secret_backend_external_ca_order_certificate.example pki/role/example-role/order/abc123/certificate
```

~> **Note** This resource polls the order status with a timeout. If the order does not complete within the polling period, the resource creation will fail. Ensure all challenges are properly fulfilled before creating this resource.