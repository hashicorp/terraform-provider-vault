---
layout: "vault"
page_title: "Vault: vault_secrets_sync_gcp_destination resource"
sidebar_current: "docs-vault-resource-secrets-sync-gcp-destination"
description: |-
  Creates a GCP destination to synchronize secrets in Vault
---

# vault\_secrets\_sync\_gcp\_destination

Creates a GCP destination to synchronize secrets in Vault. Requires Vault 1.16+.
*Available only for Vault Enterprise*.

~> **Important** All data provided in the resource configuration will be
written in cleartext to state and plan files generated by Terraform, and
will appear in the console output when Terraform runs. Protect these
artifacts accordingly. See
[the main provider documentation](../index.html)
for more details.

For more information on syncing secrets with GCP, please refer to the Vault
[documentation](https://developer.hashicorp.com/vault/docs/sync/gcpsm).

## Example Usage

```hcl
resource "vault_secrets_sync_gcp_destination" "gcp" {
  name                 = "gcp-dest"
  project_id           = "gcp-project-id"
  credentials          = file(var.credentials_file)
  secret_name_template = "vault_{{ .MountAccessor | lowercase }}_{{ .SecretPath | lowercase }}"
  custom_tags          = {
    "foo" = "bar"
  }
}
```

### With Networking Configuration (Vault 1.19+)

```hcl
resource "vault_secrets_sync_gcp_destination" "gcp_networking" {
  name                         = "gcp-dest-networking"
  project_id                   = "gcp-project-id"
  credentials                  = file(var.credentials_file)
  secret_name_template         = "vault_{{ .MountAccessor | lowercase }}_{{ .SecretPath | lowercase }}"
  
  allowed_ipv4_addresses       = ["10.0.0.0/8", "192.168.0.0/16"]
  allowed_ipv6_addresses       = ["2001:db8::/32"]
  allowed_ports                = [443, 8443]
  disable_strict_networking    = false
}
```

### With Global Encryption (Vault 1.19+)

```hcl
resource "vault_secrets_sync_gcp_destination" "gcp_encryption" {
  name                 = "gcp-dest-encryption"
  project_id           = "gcp-project-id"
  credentials          = file(var.credentials_file)
  secret_name_template = "vault_{{ .MountAccessor | lowercase }}_{{ .SecretPath | lowercase }}"
  
  global_kms_key = "projects/my-project/locations/global/keyRings/my-keyring/cryptoKeys/my-key"
}
```

### With Multi-Region Replication and Regional Encryption (Vault 1.19+)

```hcl
resource "vault_secrets_sync_gcp_destination" "gcp_replication_encryption" {
  name                 = "gcp-dest-replication-encryption"
  project_id           = "gcp-project-id"
  credentials          = file(var.credentials_file)
  secret_name_template = "vault_{{ .MountAccessor | lowercase }}_{{ .SecretPath | lowercase }}_{{ .SecretKey | lowercase }}"
  granularity          = "secret-key"
  
  locational_kms_keys = {
    "us-central1" = "projects/my-project/locations/us-central1/keyRings/kr/cryptoKeys/key"
    "us-east1"    = "projects/my-project/locations/us-east1/keyRings/kr/cryptoKeys/key"
  }
  replication_locations = ["us-central1", "us-east1"]
}
```

### Using Workload Identity Federation (Vault 2.0.0+)

```hcl
resource "vault_secrets_sync_gcp_destination" "gcp_wif" {
  name                    = "gcp-dest-wif"
  service_account_email   = var.service_account_email
  identity_token_audience = var.identity_token_audience
  identity_token_ttl      = 3600
  identity_token_key      = "my-key"
  granularity             = "secret-path"
}
```

## Argument Reference

The following arguments are supported:

* `namespace` - (Optional) The namespace to provision the resource in.
  The value should not contain leading or trailing forward slashes.
  The `namespace` is always relative to the provider's configured [namespace](/docs/providers/vault/index.html#namespace).

* `name` - (Required) Unique name of the GCP destination.

* `credentials` - (Optional) JSON-encoded credentials to use to connect to GCP.
  Can be omitted and directly provided to Vault using the `GOOGLE_APPLICATION_CREDENTIALS` environment
  variable.

* `custom_tags` - (Optional) Custom tags to set on the secret managed at the destination.

* `secret_name_template` - (Optional) Template describing how to generate external secret names.
  Supports a subset of the Go Template syntax.

* `granularity` - (Optional) Determines what level of information is synced as a distinct resource
  at the destination. Supports `secret-path` and `secret-key`.

* `project_id` - (Optional) The target project to manage secrets in. If set,
  overrides the project ID derived from the service account JSON credentials or application
  default credentials. The service account must be [authorized](https://cloud.google.com/iam/docs/service-account-overview#locations)
  to perform Secret Manager actions in the target project.


### Workload Identity Federation (Vault 2.0.0+)

* `service_account_email` â€“ (Optional) Service Account to impersonate for secrets sync workload identity federation.
  Required with `identity_token_audience`. Requires Vault 2.0.0+. *Available only for Vault Enterprise*.

* `identity_token_audience` - (Optional) The audience claim value for secrets sync identity
  tokens. Must match an allowed audience configured for the target [Workload Identity Pool](https://cloud.google.com/iam/docs/workload-identity-federation-with-other-providers#prepare).
  Mutually exclusive with `credentials`.  Requires Vault 2.0.0+. *Available only for Vault Enterprise*.

* `identity_token_ttl` - (Optional) The TTL of generated tokens. Defaults to
  1 hour. Uses [duration format strings](https://developer.hashicorp.com/vault/docs/concepts/duration-format).
  Requires Vault 2.0.0+. *Available only for Vault Enterprise*.


### Networking Configuration (Vault 1.19+)

* `allowed_ipv4_addresses` - (Optional) List of IPv4 CIDR blocks that are allowed to access the synced secrets.
  Example: `["10.0.0.0/8", "192.168.0.0/16"]`.

* `allowed_ipv6_addresses` - (Optional) List of IPv6 CIDR blocks that are allowed to access the synced secrets.
  Example: `["2001:db8::/32"]`.

* `allowed_ports` - (Optional) List of TCP ports allowed for access. If not specified, all ports are allowed.
  Example: `[443, 8443]`.

* `disable_strict_networking` - (Optional) If `true`, disables strict network access controls. Default is `false`.

### Encryption Configuration (Vault 1.19+)

* `global_kms_key` - (Optional) KMS key resource name for encrypting all secrets. 
  Format: `projects/{project}/locations/{location}/keyRings/{keyring}/cryptoKeys/{key}`.

* `locational_kms_keys` - (Optional) Map of GCP regions to KMS key resource names for regional encryption. 
  Each replication location must have a corresponding KMS key.
  
  Example:
  ```hcl
  locational_kms_keys = {
    "us-central1" = "projects/my-project/locations/us-central1/keyRings/kr/cryptoKeys/key"
    "us-east1"    = "projects/my-project/locations/us-east1/keyRings/kr/cryptoKeys/key"
  }
  ```

### Replication Configuration (Vault 1.18+)

* `replication_locations` - (Optional) List of GCP regions where secrets should be replicated. 
  Example: `["us-central1", "us-east1"]`.

## Attributes Reference

The following attributes are exported in addition to the above:

* `type` - The type of the secrets destination (`gcp-sm`).

## Import

GCP Secrets sync destinations can be imported using the `name`, e.g.

```
$ terraform import vault_secrets_sync_gcp_destination.gcp gcp-dest
```
