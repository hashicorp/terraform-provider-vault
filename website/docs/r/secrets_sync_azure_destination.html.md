---
layout: "vault"
page_title: "Vault: vault_secrets_sync_azure_destination resource"
sidebar_current: "docs-vault-resource-secrets-sync-azure-destination"
description: |-
  Creates a Azure destination to synchronize secrets in Vault
---

# vault\_secrets\_sync\_azure\_destination

Creates a Azure Key Vault destination to synchronize secrets in Vault. Requires Vault 1.16+.
*Available only for Vault Enterprise*.

~> **Important** All data provided in the resource configuration will be
written in cleartext to state and plan files generated by Terraform, and
will appear in the console output when Terraform runs. Protect these
artifacts accordingly. See
[the main provider documentation](../index.html)
for more details.

For more information on syncing secrets with Azure Key Vault, please refer to the Vault
[documentation](https://developer.hashicorp.com/vault/docs/sync/azurekv).

## Example Usage

```hcl
resource "vault_secrets_sync_azure_destination" "az" {
  name                 = "az-dest"
  key_vault_uri        = var.key_vault_uri
  client_id            = var.client_id
  client_secret        = var.client_secret
  tenant_id            = var.tenant_id
  secret_name_template = "vault_{{ .MountAccessor | lowercase }}_{{ .SecretPath | lowercase }}"
  # NEW: Networking configuration (Vault 1.19+)
  allowed_ipv4_addresses = [
    "192.168.1.1/24",   # Allow private network
    "10.0.0.1/8"        # Allow corporate network
  ]

  allowed_ipv6_addresses = [
    "2001:db9::/32"     # Allow IPv6 range
  ]

  allowed_ports = [
    443,                # HTTPS
    9443                # Alternative HTTPS port
  ]

  disable_strict_networking = false  # Enforce networking restrictions
  custom_tags = {
    "foo" = "bar"
  }
}
```

### Using Workload Identity Federation (Vault 2.0.0+)

```hcl
resource "vault_secrets_sync_azure_destination" "az_wif" {
  name                    = "az-dest-wif"
  key_vault_uri           = var.key_vault_uri
  client_id               = var.client_id
  tenant_id               = var.tenant_id
  identity_token_audience = var.identity_token_audience
  identity_token_ttl      = 3600
  identity_token_key      = "my-key"
  granularity             = "secret-path"
}
```

## Argument Reference

The following arguments are supported:

* `namespace` - (Optional) The namespace to provision the resource in.
  The value should not contain leading or trailing forward slashes.
  The `namespace` is always relative to the provider's configured [namespace](/docs/providers/vault/index.html#namespace).

* `name` - (Required) Unique name of the Azure destination.

* `key_vault_uri` - (Optional) URI of an existing Azure Key Vault instance.
  Can be omitted and directly provided to Vault using the `KEY_VAULT_URI` environment
  variable.

* `tenant_id` - (Optional) ID of the target Azure tenant.
  Can be omitted and directly provided to Vault using the `AZURE_TENANT_ID` environment
  variable.

* `client_id` - (Optional) Client ID of an Azure app registration.
  Can be omitted and directly provided to Vault using the `AZURE_CLIENT_ID` environment
  variable.

* `client_secret` - (Optional) Client Secret of an Azure app registration.
  Can be omitted and directly provided to Vault using the `AZURE_CLIENT_SECRET` environment
  variable.

* `cloud` - (Optional) Specifies a cloud for the client. The default is Azure Public Cloud.

* `custom_tags` - (Optional) Custom tags to set on the secret managed at the destination.

* `secret_name_template` - (Optional) Template describing how to generate external secret names.
  Supports a subset of the Go Template syntax.

* `granularity` - (Optional) Determines what level of information is synced as a distinct resource
  at the destination. Supports `secret-path` and `secret-key`.

* `allowed_ipv4_addresses` - (Optional) List of IPv4 addresses or CIDR blocks allowed to make outbound
  connections from Vault to the destination. Requires Vault 1.19+.

* `allowed_ipv6_addresses` - (Optional) List of IPv6 addresses or CIDR blocks allowed to make outbound
  connections from Vault to the destination. Requires Vault 1.19+.

* `allowed_ports` - (Optional) List of port numbers allowed for outbound connections from Vault to the
  destination. Requires Vault 1.19+.

* `disable_strict_networking` - (Optional) When set to `true`, disables strict enforcement of networking
  restrictions. Defaults to `false`. Requires Vault 1.19+.


### Workload Identity Federation (Vault 2.0.0+)

* `identity_token_audience` - (Optional) The audience claim value for identity tokens.
  **Requires Vault 2.0.0+**.

* `identity_token_ttl` - (Optional) The TTL of generated identity tokens in seconds.
  **Requires Vault 2.0.0+**.

* `identity_token_key` - (Optional) The key to use for signing identity tokens.
  **Requires Vault 2.0.0+**.

## Attributes Reference

The following attributes are exported in addition to the above:

* `type` - The type of the secrets destination (`azure-kv`).

## Import

Azure Secrets sync destinations can be imported using the `name`, e.g.

```
$ terraform import vault_secrets_sync_azure_destination.az az-dest
```
