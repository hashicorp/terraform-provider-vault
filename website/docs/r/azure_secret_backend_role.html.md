---
layout: "vault"
page_title: "Vault: vault_azure_secret_backend_role resource"
sidebar_current: "docs-vault-resource-azure-secret-backend-role"
description: |-
  Creates an azure secret backend role for Vault.
---

# vault\_azure\_secret\_backend\_role

Creates an Azure Secret Backend Role for Vault.

The Azure secrets engine dynamically generates Azure service principals and role assignments. Vault roles can be mapped to one or more Azure roles, providing a simple, flexible way to manage the permissions granted to generated service principals.

~> **Important** All data provided in the resource configuration will be
written in cleartext to state and plan files generated by Terraform, and
will appear in the console output when Terraform runs. Protect these
artifacts accordingly. See
[the main provider documentation](../index.html)
for more details.

## Example Usage

```hcl
resource "vault_azure_secret_backend" "azure" {
  subscription_id = var.subscription_id
  tenant_id       = var.tenant_id
  client_secret   = var.client_secret
  client_id       = var.client_id
}

resource "vault_azure_secret_backend_role" "generated_role" {
  backend                     = vault_azure_secret_backend.azure.path
  role                        = "generated_role"
  sign_in_audience            = "AzureADMyOrg"
  tags                        = ["team:engineering","environment:development"]
  ttl                         = 300
  max_ttl                     = 600

  azure_roles {
    role_name = "Reader"
    scope =  "/subscriptions/${var.subscription_id}/resourceGroups/azure-vault-group"
  }
}

resource "vault_azure_secret_backend_role" "existing_object_id" {
  backend               = vault_azure_secret_backend.azure.path
  role                  = "existing_object_id"
  application_object_id = "11111111-2222-3333-4444-44444444444"
  ttl                   = 300
  max_ttl               = 600
}
```

## Argument Reference

The following arguments are supported:

* `namespace` - (Optional) The namespace to provision the resource in.
  The value should not contain leading or trailing forward slashes.
  The `namespace` is always relative to the provider's configured [namespace](/docs/providers/vault/index.html#namespace).
  *Available only for Vault Enterprise*.

* `role` - (Required) Name of the Azure role
* `backend` - (Optional) Path to the mounted Azure auth backend
* `azure_groups` - (Optional) List of Azure groups to be assigned to the generated service principal.
* `azure_roles` - (Optional) List of Azure roles to be assigned to the generated service principal.
* `application_object_id` - (Optional) Application Object ID for an existing service principal that will
  be used instead of creating dynamic service principals. If present, `azure_roles` and `permanently_delete` will be ignored.
* `permanently_delete` - (Optional) Indicates whether the applications and service principals created by Vault will be permanently
  deleted when the corresponding leases expire. Defaults to `false`. For Vault v1.12+.
* `ttl` – (Optional) Specifies the default TTL for service principals generated using this role.
  Accepts time suffixed strings ("1h") or an integer number of seconds. Defaults to the system/engine default TTL time.
* `max_ttl` – (Optional) Specifies the maximum TTL for service principals generated using this role. Accepts time
  suffixed strings ("1h") or an integer number of seconds. Defaults to the system/engine max TTL time.
* `explicit_max_ttl` - (Optional) Specifies the explicit maximum lifetime of the lease and service principal generated using this role. If not set or set to 0, will use the system default (10 years). Requires Vault 1.18+.
* `sign_in_audience` - (Optional) Specifies the security principal types that are allowed to sign in to the application.
  Valid values are: AzureADMyOrg, AzureADMultipleOrgs, AzureADandPersonalMicrosoftAccount, PersonalMicrosoftAccount. Requires Vault 1.16+.
* `tags` - (Optional) - A list of Azure tags to attach to an application. Requires Vault 1.16+.
* `persist_app` - (Optional) If set to true, persists the created service principal and application for the lifetime of the role

## Attributes Reference

No additional attributes are exported by this resource.
